rule concoct_cut_contigs:
    input:
        fasta = "{sample}/DeepMicroClass/all_prokaryotic_seqs.fa",
        bam = "{sample}/{sample}_metaG.reads.sorted.bam",
        bai = "{sample}/{sample}_metaG.reads.sorted.bam.bai"
    output:
        split_contigs_fasta = "{sample}/concoct/split_contigs_len-%s.fa" % config["concoct"]["contig_split_length"],
        split_contigs_bed = "{sample}/concoct/split_contigs_len-%s.bed" % config["concoct"]["contig_split_length"]
    params:
        contig_split_length = config["concoct"]["contig_split_length"]
    conda: f"{env_dir}/concoct_env.yml"
    resources: 
        mem_mb = config["concoct"]["mem_mb"],
        runtime = config["concoct"]["runtime"]
    log: "logs/concoct/{sample}_cut_contigs.log"
    shell:
        """
        cut_up_fasta.py {input.fasta} -c {params.contig_split_length} -o 0 --merge_last -b {output.split_contigs_bed} > {output.split_contigs_fasta} \
        2> {log}
        """

rule concoct_coverage_table:
    input:
        split_contigs_bed = "{sample}/concoct/split_contigs_len-%s.bed" % config["concoct"]["contig_split_length"],
        bam = "{sample}/{sample}_metaG.reads.sorted.bam",
        bai = "{sample}/{sample}_metaG.reads.sorted.bam.bai"
    output:
        coverage_table = "{sample}/concoct/split_contigs_len-%s_coverage_table.tsv" % config["concoct"]["contig_split_length"]
    params:
        contig_split_length = config["concoct"]["contig_split_length"]
    conda: f"{env_dir}/concoct_env.yml"
    resources:
        mem_mb = config["concoct"]["mem_mb"],
        runtime = config["concoct"]["runtime"]
    log: "logs/concoct/{sample}_coverage_table.log"
    shell:
        """
        concoct_coverage_table.py {input.split_contigs_bed} {input.bam} > {output.coverage_table} \
        2> {log}
        """

rule concoct:
    input:
        split_contigs_fasta = "{sample}/concoct/split_contigs_len-%s.fa" % config["concoct"]["contig_split_length"],
        coverage_table = "{sample}/concoct/split_contigs_len-%s_coverage_table.tsv" % config["concoct"]["contig_split_length"]
    output:
        clustering_result = "{sample}/concoct/results_clustering_gt%s.csv" % config["binning"]["min_contig_length"]
    params:
        contig_min_length = config["binning"]["min_contig_length"]
    threads: config["concoct"]["threads"]
    conda: f"{env_dir}/concoct_env.yml"
    resources:
        mem_mb = config["concoct"]["mem_mb_cluster"],
        runtime = config["concoct"]["runtime_cluster"]
    log: "logs/concoct/{sample}_cluster.log"
    shell:
        """
        concoct --composition_file {input.split_contigs_fasta} -l {params.contig_min_length} \
        --coverage_file {input.coverage_table} -t {threads} -b {wildcards.sample}/concoct/results \
        2> {log}
        """
 
rule concoct_merge_clusters:
    input:
        clustering_result = "{sample}/concoct/results_clustering_gt%s.csv" % config["binning"]["min_contig_length"],
        fasta = "{sample}/DeepMicroClass/all_prokaryotic_seqs.fa"
    output:
        merged_table = "{sample}/concoct/clustering_merged.csv",
        bin_dir = directory("{sample}/concoct/bins"),
        done = "{sample}/concoct/concoct.done"
    conda: f"{env_dir}/concoct_env.yml"
    resources:
        mem_mb = config["concoct"]["mem_mb"],
        runtime = config["concoct"]["runtime"]
    log: "logs/concoct/{sample}_merge_clusters.log"
    shell:
        """
        merge_cutup_clustering.py {input.clustering_result} > {output.merged_table} \
        2> {log}

        mkdir -p {output.bin_dir}

        extract_fasta_bins.py {input.fasta} {output.merged_table} --output_path {output.bin_dir} \
        2>> {log}

        touch {output.done}
        """

rule concoct_contig_to_bin:
    input:
        merged_table = "{sample}/concoct/clustering_merged.csv",
        done = "{sample}/concoct/concoct.done"
    output:
        contig_to_bin="{sample}/concoct/contig_to_bin.tsv"
    resources:
        mem_mb = config["concoct"]["mem_mb"],
        runtime = config["concoct"]["runtime"]
    resources:
        mem_mb = 8000,
        runtime = 120
    log: "logs/concoct/{sample}_contig_to_bin.log"
    shell:
        """
        paste <(cut -f2 -d',' {input.merged_table}) \
              <(cut -f1 -d',' {input.merged_table}) | \
        awk '{{print $0 "\tconcoct"}}' | \
        tail -n +2 | \
        sort > {output.contig_to_bin} 2> {log}
        """
