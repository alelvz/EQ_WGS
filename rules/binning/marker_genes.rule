rule magscot_marker_genes:
    input:
        contigs = "{sample}/DeepMicroClass/all_prokaryotic_seqs.fa"
    output:
        faa = "{sample}/magscot/prodigal.faa",
        ffn = "{sample}/magscot/prodigal.fna",
        tigr_out = temp("{sample}/magscot/hmm.tigr.out"),
        tigr_tab = temp("{sample}/magscot/hmm.tigr.hit.out"),
        pfam_out = temp("{sample}/magscot/hmm.pfam.out"),
        pfam_tab = temp("{sample}/magscot/hmm.pfam.hit.out"),
        markers_hmm = "{sample}/magscot/markers.hmm"
    params: 
        magscot_db = config["magscot"]["db"],
        temp_prefix = "{sample}/magscot/prodigal_temp"
    resources:
        runtime = config["prodigal"]["runtime"],
        mem_mb = config["prodigal"]["mem_mb"]
    threads: config["magscot"]["threads"]
    conda: f"{env_dir}/magscot_env.yml"
    log: "logs/magscot/{sample}_marker_genes.log"
    shell: 
        """
        mkdir -p {wildcards.sample}/magscot
        
        cat {input.contigs} | parallel -j {threads} --block 1M --recstart '>' --pipe \
        prodigal -p meta -q -a {params.temp_prefix}.{{#}}.faa -d {params.temp_prefix}.{{#}}.ffn -o /dev/null \
        2>> {log}

        cat {params.temp_prefix}.*.faa > {output.faa} 2>> {log}
        cat {params.temp_prefix}.*.ffn > {output.ffn} 2>> {log}
        rm -f {params.temp_prefix}.* 2>> {log}
        
        hmmsearch -o {output.tigr_out} --tblout {output.tigr_tab} --noali --notextw --cut_nc --cpu {threads} \
        {params.magscot_db}/hmm/gtdbtk_rel207_tigrfam.hmm {output.faa} 2>> {log}

        hmmsearch -o {output.pfam_out} --tblout {output.pfam_tab} --noali --notextw --cut_nc --cpu {threads} \
        {params.magscot_db}/hmm/gtdbtk_rel207_Pfam-A.hmm {output.faa} 2>> {log}
        
        cat {output.tigr_tab} | grep -v "^#" | awk '{{print $1"\t"$4"\t"$5}}' > {output.markers_hmm} 2>> {log}
        cat {output.pfam_tab} | grep -v "^#" | awk '{{print $1"\t"$4"\t"$5}}' >> {output.markers_hmm} 2>> {log}
        """
