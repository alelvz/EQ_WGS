rule maxbin2:
    input:
        fasta = "{sample}/DeepMicroClass/all_prokaryotic_seqs.fa",
        depth_file = "{sample}/contig_depth.txt"
    output:
        bin_dir = directory('{sample}/maxbin2/bins'),
        done = '{sample}/maxbin2/maxbin2.done'
    params:
        min_contig_length = config["binning"]["min_contig_length"],
        prefix = '{sample}/maxbin2/bins/bin'
    threads: config["maxbin2"]["threads"]
    conda: f"{env_dir}/maxbin2_env.yml"
    resources:
        mem_mb = config["maxbin2"]["mem_mb"],
        runtime = config["maxbin2"]["runtime"]
    log: "logs/maxbin2/{sample}_maxbin2.log"
    shell:
        """
        mkdir -p {output.bin_dir}

	    run_MaxBin.pl -contig {input.fasta} \
        -thread {threads} \
        -abund {input.depth_file} \
        -min_contig_length {params.min_contig_length} \
        -out {params.prefix} \
        2> {log}

        touch {output.done}
        """

rule maxbin2_contig_to_bin:
    input:
        bin_dir = "{sample}/maxbin2/bins",
        done = "{sample}/maxbin2/maxbin2.done"
    output:
        contig_to_bin="{sample}/maxbin2/contig_to_bin.tsv",
    resources:
        mem_mb = 8000,
        runtime = 120
    log: "logs/maxbin2/{sample}_contig_to_bin.log"
    shell:
        """
        > {output.contig_to_bin}
        
        for bin_file in {input.bin_dir}/*.fasta; do
            if [ -f "$bin_file" ]; then
                bin_name=$(basename "$bin_file" .fasta)
                grep "^>" "$bin_file" | sed 's/^>//' | \
                    awk -v bin="$bin_name" '{{print bin "\t" $1 "\tmaxbin2"}}' \
                    >> {output.contig_to_bin}
            fi
        done
        
        sort -k1,1 {output.contig_to_bin} -o {output.contig_to_bin} 2> {log}
        """

