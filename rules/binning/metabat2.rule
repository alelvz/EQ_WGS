rule get_depth_of_coverage:
    input:
        bam = "{sample}/{sample}_metaG.reads.sorted.bam",
        bai = "{sample}/{sample}_metaG.reads.sorted.bam.bai"
    output:
        depth_file = "{sample}/contig_depth.txt",
    conda: f"{env_dir}/metabat2_env.yml"
    resources:
        mem_mb = config["metabat2"]["mem_mb"],
        runtime = config["metabat2"]["runtime"]
    log: "logs/metabat2/{sample}_jgi_summarize_bam_contig_depths.log"
    shell:
        """
	    jgi_summarize_bam_contig_depths --outputDepth {output.depth_file} {input.bam} \
        2> {log}
        """

rule metabat2:
    input:
        fasta = "{sample}/DeepMicroClass/all_prokaryotic_seqs.fa",
        bam = "{sample}/{sample}_metaG.reads.sorted.bam",
        bai = "{sample}/{sample}_metaG.reads.sorted.bam.bai",
        depth_file = "{sample}/contig_depth.txt"
    output:
        bin_dir = directory('{sample}/metabat2/bins'),
        done = '{sample}/metabat2/metabat2.done'
    params:
        min_contig_length = config["binning"]["min_contig_length"],
        prefix = '{sample}/metabat2/bins/bin'
    resources:
        mem_mb = config["metabat2"]["mem_mb"],
        runtime = config["metabat2"]["runtime"]
    threads: config["metabat2"]["threads"]
    conda: f"{env_dir}/metabat2_env.yml"
    log: "logs/metabat2/{sample}_metabat2.log"
    shell:
        """
        mkdir -p {output.bin_dir}

        metabat2 -t {threads} \
        -i {input.fasta} \
        -o {params.prefix} \
        -a {input.depth_file} \
        -m {params.min_contig_length} \
        2> {log}

	    touch {output.done}
        """

rule metabat2_contig_to_bin:
    input:
        bin_dir = "{sample}/metabat2/bins",
        done = "{sample}/metabat2/metabat2.done"
    output:
        contig_to_bin="{sample}/metabat2/contig_to_bin.tsv"
    resources:
        mem_mb = 8000,
        runtime = 120
    log: "logs/metabat2/{sample}_contig_to_bin.log"
    shell:
        """
        > {output.contig_to_bin}
        
        for bin_file in {input.bin_dir}/*.fa; do
            if [ -f "$bin_file" ]; then
                bin_name=$(basename "$bin_file" .fa)
                grep "^>" "$bin_file" | sed 's/^>//' | \
                    awk -v bin="$bin_name" '{{print bin "\t" $1 "\tmetabat2"}}' \
                    >> {output.contig_to_bin}
            fi
        done
        
        sort -k1,1 {output.contig_to_bin} -o {output.contig_to_bin} 2> {log}
        """

