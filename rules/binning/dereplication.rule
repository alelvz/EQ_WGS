rule drep_collect_all_bins:
    input:
        sample_bins = expand("{sample}/magscot_bins", sample = samples)
    output:
        bins = "bin_paths.txt"
    threads: 1
    resources:
        runtime = 1440,
        mem_mb = 8000
    log: "logs/drep/collect_all_bins.log"
    shell:
        """
        find {input} -type f | grep ".fasta$" > {output.bins} 2> {log}
        """

rule drep_dereplication:
    input:
        bins = "bin_paths.txt"
    output:
        output_dir = directory("dereplication")
    params: 
        comp = config["drep"]["completeness"],
        con = config["drep"]["contamination"],
        strw = config["drep"]["strain_heterogeneity_weight"],
        pani = config["drep"]["P_ani"],
        sani = config["drep"]["S_ani"]
    threads: config["drep"]["threads"]
    resources:
        mem_mb = config["drep"]["mem_mb"],
        runtime = config["drep"]["runtime"]
    conda: f"{env_dir}/drep_env.yml"
    log: "logs/drep/dereplication.log"
    shell: 
        """
        dRep dereplicate \
        {output.output_dir} \
        -p {threads} \
        -comp {params.comp} \
        -con {params.con} \
        -strW {params.strw} \
        --P_ani {params.pani} \
        --S_ani {params.sani} \
        --run_tertiary_clustering -centW 0 \
        -g {input.bins} \
        2> {log}
        """
